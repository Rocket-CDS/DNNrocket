/************************************************************/
/*****              SqlDataProvider                     *****/
/*****                                                  *****/
/*****                                                  *****/
/***** Note: To manually execute this script you must   *****/
/*****       perform a search and replace operation     *****/
/*****       for {databaseOwner} and {objectQualifier}  *****/
/*****                                                  *****/
/************************************************************/



/************************************************************/
/*****              SqlDataProvider                     *****/
/************************************************************/

-------------------------------------------------------------------------------
--------------                       TABLES                        ------------
-------------------------------------------------------------------------------
-- CREATE DNNrocket
if NOT exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}[{objectQualifier}DNNrocket]') and OBJECTPROPERTY(id, N'IsUserTable') = 1)
BEGIN

CREATE TABLE {databaseOwner}[{objectQualifier}DNNrocket] (
	[ItemId] [int] IDENTITY (1, 1) NOT NULL ,
	[PortalId] [int] NULL ,
	[ModuleId] [int] NULL ,
	[TypeCode] [nvarchar](50) NULL ,
	[XMLData] [xml] NULL ,
	[GUIDKey] [nvarchar](250) NULL ,
	[ModifiedDate] [datetime] NULL ,
	[TextData] [nvarchar](MAX) NULL ,
	[XrefItemId] [int] NULL ,
	[ParentItemId] [int] NULL ,
	[Lang] [nvarchar] (50) NULL ,
	[UserId] [int] NOT NULL CONSTRAINT [DF_DNNrocket_UserId] DEFAULT ((-1))
	CONSTRAINT [PK_DNNrocket] PRIMARY KEY  CLUSTERED 
	(
		[ItemId]
	)  ON [PRIMARY] 
) ON [PRIMARY]

-- Index DNNrocket
 CREATE NONCLUSTERED INDEX IX_DNNrocketXref ON {databaseOwner}[{objectQualifier}DNNrocket] (  XrefItemId ASC  )   WITH (  PAD_INDEX = OFF ,FILLFACTOR = 100  ,SORT_IN_TEMPDB = OFF , IGNORE_DUP_KEY = OFF , STATISTICS_NORECOMPUTE = OFF , DROP_EXISTING = OFF , ONLINE = OFF , ALLOW_ROW_LOCKS = ON , ALLOW_PAGE_LOCKS = ON  ) ON [PRIMARY ]  
 CREATE NONCLUSTERED INDEX IX_DNNrocketMod ON {databaseOwner}[{objectQualifier}DNNrocket] (  ModuleId ASC  )   WITH (  PAD_INDEX = OFF ,FILLFACTOR = 100  ,SORT_IN_TEMPDB = OFF , IGNORE_DUP_KEY = OFF , STATISTICS_NORECOMPUTE = OFF , DROP_EXISTING = OFF , ONLINE = OFF , ALLOW_ROW_LOCKS = ON , ALLOW_PAGE_LOCKS = ON  ) ON [PRIMARY ] 
 CREATE NONCLUSTERED INDEX IX_DNNrocketParent ON {databaseOwner}[{objectQualifier}DNNrocket] (  ParentItemId ASC  )   WITH (  PAD_INDEX = OFF ,FILLFACTOR = 100  ,SORT_IN_TEMPDB = OFF , IGNORE_DUP_KEY = OFF , STATISTICS_NORECOMPUTE = OFF , DROP_EXISTING = OFF , ONLINE = OFF , ALLOW_ROW_LOCKS = ON , ALLOW_PAGE_LOCKS = ON  ) ON [PRIMARY ] 
 CREATE NONCLUSTERED INDEX IX_DNNrocketPortal ON {databaseOwner}[{objectQualifier}DNNrocket] (  PortalId ASC  )   WITH (  PAD_INDEX = OFF ,FILLFACTOR = 100  ,SORT_IN_TEMPDB = OFF , IGNORE_DUP_KEY = OFF , STATISTICS_NORECOMPUTE = OFF , DROP_EXISTING = OFF , ONLINE = OFF , ALLOW_ROW_LOCKS = ON , ALLOW_PAGE_LOCKS = ON  ) ON [PRIMARY ] 
 CREATE NONCLUSTERED INDEX IX_DNNrocketType ON {databaseOwner}[{objectQualifier}DNNrocket] (  TypeCode ASC  )   WITH (  PAD_INDEX = OFF ,FILLFACTOR = 100  ,SORT_IN_TEMPDB = OFF , IGNORE_DUP_KEY = OFF , STATISTICS_NORECOMPUTE = OFF , DROP_EXISTING = OFF , ONLINE = OFF , ALLOW_ROW_LOCKS = ON , ALLOW_PAGE_LOCKS = ON  ) ON [PRIMARY ] 
 CREATE NONCLUSTERED INDEX IX_DNNrocketUserId ON {databaseOwner}[{objectQualifier}DNNrocket] (  UserId ASC  )   WITH (  PAD_INDEX = OFF ,FILLFACTOR = 100  ,SORT_IN_TEMPDB = OFF , IGNORE_DUP_KEY = OFF , STATISTICS_NORECOMPUTE = OFF , DROP_EXISTING = OFF , ONLINE = OFF , ALLOW_ROW_LOCKS = ON , ALLOW_PAGE_LOCKS = ON  ) ON [PRIMARY ] 
 CREATE NONCLUSTERED INDEX IX_DNNrocketLang ON {databaseOwner}[{objectQualifier}DNNrocket] (  Lang ASC  )   WITH (  PAD_INDEX = OFF ,FILLFACTOR = 100  ,SORT_IN_TEMPDB = OFF , IGNORE_DUP_KEY = OFF , STATISTICS_NORECOMPUTE = OFF , DROP_EXISTING = OFF , ONLINE = OFF , ALLOW_ROW_LOCKS = ON , ALLOW_PAGE_LOCKS = ON  ) ON [PRIMARY ] 
 CREATE NONCLUSTERED INDEX IX_DNNrocketGuidKey ON {databaseOwner}[{objectQualifier}DNNrocket] (  GuidKey ASC  )   WITH (  PAD_INDEX = OFF ,FILLFACTOR = 100  ,SORT_IN_TEMPDB = OFF , IGNORE_DUP_KEY = OFF , STATISTICS_NORECOMPUTE = OFF , DROP_EXISTING = OFF , ONLINE = OFF , ALLOW_ROW_LOCKS = ON , ALLOW_PAGE_LOCKS = ON  ) ON [PRIMARY ] 

END

GO


-------------------------------------------------------------------------------
--------------                       FUNCTIONS                     ------------
-------------------------------------------------------------------------------


SET QUOTED_IDENTIFIER ON 
GO
SET ANSI_NULLS ON 
GO

if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}[{objectQualifier}DNNrocketLangMerge]') and OBJECTPROPERTY(id, N'IsScalarFunction') = 1)
drop function {databaseOwner}[{objectQualifier}DNNrocketLangMerge]
GO

CREATE FUNCTION {databaseOwner}[{objectQualifier}DNNrocketLangMerge](@xmllangdata AS XML,@xmlbasedata AS XML)
RETURNS XML
BEGIN

DECLARE @rtndata AS XML

IF NOT @xmlbasedata IS NULL
BEGIN
	IF NOT @xmllangdata IS NULL
	BEGIN
		SET @xmlbasedata.modify('insert <lang/> as last into /genxml[1]')
		SET @xmlbasedata.modify('insert sql:variable("@xmllangdata") as last into /genxml[1]/lang[1]')
	END
	SET @rtndata = @xmlbasedata
END
ELSE
BEGIN
	-- is not a language record so just return the language data
	SET @rtndata = ISNULL(@xmllangdata,'')
END

RETURN @rtndata

END

GO

SET QUOTED_IDENTIFIER OFF 
GO
SET ANSI_NULLS ON 
GO



-------------------------------------------------------------------------------
--------------                       SPROCS                        ------------
-------------------------------------------------------------------------------

SET QUOTED_IDENTIFIER ON 

GO
SET ANSI_NULLS ON 
GO


if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}[{objectQualifier}DNNrocket_CleanData]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
drop procedure {databaseOwner}[{objectQualifier}DNNrocket_CleanData]
GO

CREATE   PROCEDURE {databaseOwner}[{objectQualifier}DNNrocket_CleanData]
AS
begin

/***  Clean ALL data that is not linked to a module.  i.e. modules that have been deleted. **/
delete from {databaseOwner}{objectQualifier}DNNrocket where moduleid not in (select moduleid from {databaseOwner}{objectQualifier}Modules) and moduleid > 0 
	
end

GO

if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}[{objectQualifier}DNNrocket_Delete]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
drop procedure {databaseOwner}[{objectQualifier}DNNrocket_Delete]
GO



CREATE   PROCEDURE {databaseOwner}[{objectQualifier}DNNrocket_Delete]
@ItemID int
AS
begin
	
	DECLARE @DelItemId int	

	-- Delete all linked child records.
	DECLARE delete_cursor CURSOR LOCAL FOR 
	select ItemId from {databaseOwner}[{objectQualifier}DNNrocket] where ParentItemId = @ItemId

	OPEN delete_cursor
	FETCH NEXT FROM delete_cursor INTO @DelItemId
	WHILE @@FETCH_STATUS = 0
	BEGIN
		EXEC {databaseOwner}[{objectQualifier}DNNrocket_Delete] @DelItemId
		FETCH NEXT FROM delete_cursor INTO @DelItemId
	END
	CLOSE delete_cursor;
	DEALLOCATE delete_cursor;

	-- Delete all linked child records (xrefitemid).
	DECLARE delete_cursor1 CURSOR LOCAL FOR 
	select ItemId from {databaseOwner}[{objectQualifier}DNNrocket] where XrefItemId = @ItemId

	OPEN delete_cursor1
	FETCH NEXT FROM delete_cursor1 INTO @DelItemId
	WHILE @@FETCH_STATUS = 0
	BEGIN
		EXEC {databaseOwner}[{objectQualifier}DNNrocket_Delete] @DelItemId
		FETCH NEXT FROM delete_cursor1 INTO @DelItemId
	END
	CLOSE delete_cursor1;
	DEALLOCATE delete_cursor1;


	delete from {databaseOwner}[{objectQualifier}DNNrocket] where ItemId = @ItemId
	
end
GO


if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}[{objectQualifier}DNNrocket_Update]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
drop procedure {databaseOwner}[{objectQualifier}DNNrocket_Update]
GO


CREATE   PROCEDURE {databaseOwner}[{objectQualifier}DNNrocket_Update]
(
@ItemId int,
@PortalId int, 
@ModuleId int,
@TypeCode nvarchar(50),
@XMLData xml,
@GUIDKey nvarchar(250),
@ModifiedDate datetime,
@TextData nvarchar(MAX),
@XrefItemId int,
@ParentItemId int,
@UserId int,
@Lang nvarchar(10)
)
AS
BEGIN

	if not exists (select ItemID from {databaseOwner}[{objectQualifier}DNNrocket] where ItemID = @ItemID
 )
	begin
		insert into {databaseOwner}[{objectQualifier}DNNrocket]
		(
PortalId, 
ModuleId,
TypeCode,
XMLData,
GUIDKey,
ModifiedDate,
TextData,
XrefItemId,
ParentItemId,
UserId,
Lang
		)
		values
		(
@PortalId, 
@ModuleId,
@TypeCode,
@XMLData,
@GUIDKey,
@ModifiedDate,
@TextData,
@XrefItemId,
@ParentItemId,
@UserId,
@Lang
		)
		
		set @ItemID = @@IDENTITY

	end
	else
	begin
		Update {databaseOwner}[{objectQualifier}DNNrocket]
		set 
PortalId = @PortalId, 
ModuleId = @ModuleId,
TypeCode = @TypeCode,
XMLData = @XMLData,
GUIDKey = @GUIDKey,
ModifiedDate = @ModifiedDate,
TextData = @TextData,
XrefItemId = @XrefItemId,
ParentItemId = @ParentItemId,
UserId = @UserId,
Lang = @Lang
		where ItemId = @ItemId
 
	end
	
	select @ItemID

END

GO


if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}[{objectQualifier}DNNrocket_Get]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
drop procedure {databaseOwner}[{objectQualifier}DNNrocket_Get]
GO


CREATE     PROCEDURE {databaseOwner}[{objectQualifier}DNNrocket_Get]
@ItemID int,
@Lang nvarchar(10)
AS
begin
	select
	R1.[ItemId]
	,R1.[PortalId]
	,R1.[ModuleId]
	,R1.[TypeCode]
	,{databaseOwner}[{objectQualifier}DNNrocketLangMerge](RLang1.[XMLData],R1.[XMLData]) as [XMLData]
	,R1.[GUIDKey]
	,R1.[ModifiedDate]
	,R1.[TextData]
	,R1.[XrefItemId]
	,R1.[ParentItemId]
	,ISNULL(RLang1.[Lang],ISNULL(R1.[Lang],'')) as [Lang] 
	,R1.[UserId]
	from {databaseOwner}[{objectQualifier}DNNrocket] as R1
	left join dbo.[DNNrocket] as RLang1 on R1.Itemid = RLang1.ParentItemId and RLang1.Lang = @Lang  and RLang1.TypeCode = R1.TypeCode + 'LANG'
	where R1.ItemId = @ItemId
end

GO




if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}[{objectQualifier}DNNrocket_GetRecord]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
drop procedure {databaseOwner}[{objectQualifier}DNNrocket_GetRecord]
GO

CREATE     PROCEDURE {databaseOwner}[{objectQualifier}DNNrocket_GetRecord]
@ItemID int
AS
begin
	select
	[ItemId]
	,[PortalId]
	,[ModuleId]
	,[TypeCode]
	,[XMLData]
	,[GUIDKey]
	,[ModifiedDate]
	,[TextData]
	,[XrefItemId]
	,[ParentItemId]
	,[Lang] 
	,[UserId]
	from {databaseOwner}[{objectQualifier}DNNrocket] as R1
	where R1.ItemId = @ItemId
end
GO

if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}[{objectQualifier}DNNrocket_GetRecordLang]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
drop procedure {databaseOwner}[{objectQualifier}DNNrocket_GetRecordLang]
GO

CREATE     PROCEDURE {databaseOwner}[{objectQualifier}DNNrocket_GetRecordLang]
@ParentItemId int,
@Lang nvarchar(10)
AS
begin
	select
	[ItemId]
	,[PortalId]
	,[ModuleId]
	,[TypeCode]
	,[XMLData]
	,[GUIDKey]
	,[ModifiedDate]
	,[TextData]
	,[XrefItemId]
	,[ParentItemId]
	,[Lang] 
	,[UserId]
	from {databaseOwner}[{objectQualifier}DNNrocket] as R1
	where R1.ParentItemId = @ParentItemId and R1.Lang = @Lang
end
GO




if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}[{objectQualifier}DNNrocket_GetList]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
drop procedure {databaseOwner}[{objectQualifier}DNNrocket_GetList]
GO

CREATE    PROCEDURE {databaseOwner}[{objectQualifier}DNNrocket_GetList]
@PortalId int, 
@ModuleId int,
@TypeCode nvarchar(50),
@Filter nvarchar(1000),
@OrderBy nvarchar(500),
@ReturnLimit int = 0,
@pageNum int = 0,
@PageSize int = 0,
@RecordCount int = 0,
@Lang nvarchar(50) = ''

AS
begin

	SET NOCOUNT ON
	 
	SET @Filter = REPLACE(@Filter,'[XMLData]','ISNULL([RLangIdx].[XMLData],{databaseOwner}[{objectQualifier}DNNrocketLangMerge](RLang1.[XMLData],R1.[XMLData]))')


------------------------------------------------------------------------------------
--- Cursor for INDEX records.
------------------------------------------------------------------------------------
DECLARE @idxname nvarchar(250) = ''
DECLARE @TCode nvarchar(250) = ''
DECLARE @idxparentItemId int = 0
DECLARE @systemlinkItemId int = 0
DECLARE @JoinIndex nvarchar(max)

select @systemlinkItemId = itemid from {databaseOwner}[{objectQualifier}DNNrocket] where [TypeCode] = 'SYSTEMLINK' and GUIDkey = @TypeCode 

--print '@systemlinkItemId: ' + convert(nvarchar(max),@systemlinkItemId)

DECLARE idx_cursor CURSOR FOR 
SELECT GUIDKey, ParentItemId, textdata
FROM {databaseOwner}[{objectQualifier}DNNrocket]
WHERE [TypeCode] like 'SYSTEMLINK%' and ParentItemId = @systemlinkItemId

SET @JoinIndex = ''

OPEN idx_cursor  
FETCH NEXT FROM idx_cursor INTO @idxname, @idxparentItemId, @TCode

WHILE @@FETCH_STATUS = 0  
BEGIN  

	if (CHARINDEX(@idxname, @Filter) > 0)
	begin

		set @JoinIndex = @JoinIndex + ' left join {databaseOwner}[{objectQualifier}DNNrocket] as [' + @idxname + '] on [' + @idxname + '].XrefItemId = R1.ItemId and [' + @idxname + '].TypeCode = ''IDX_' + @idxname + ''' and ([' + @idxname + '].[Lang] = ''' + @Lang + ''' or [' + @idxname + '].[Lang] = '''') '

	end

	FETCH NEXT FROM idx_cursor INTO @idxname,@idxparentItemId, @TCode
END 

CLOSE idx_cursor
DEALLOCATE idx_cursor

--print '@JoinIndex: ' + @JoinIndex

------------------------------------------------------------------------------------
------------------------------------------------------------------------------------



	 
	 
	 
	 DECLARE
		 @STMT nvarchar(max)         -- SQL to execute
		,@rtnFields nvarchar(max)

	IF (@PortalId >= 0) BEGIN

		IF (@ModuleId >= 0) BEGIN
			SET @Filter = ' and (R1.PortalId = '''  + Convert(nvarchar(10),@PortalId) + ''' or R1.PortalId = ''-1'') and (R1.ModuleId = ''' + Convert(nvarchar(10),@ModuleId) + ''' or R1.ModuleId = ''-1'') ' + @Filter
		END ELSE BEGIN
			SET @Filter = ' and (R1.PortalId = '''  + Convert(nvarchar(10),@PortalId) + '''  or R1.PortalId = ''-1'') ' + @Filter
		END 

	END 

	SET @OrderBy = REPLACE(@OrderBy,'[XMLData]','ISNULL([RLangIdx].[XMLData],{databaseOwner}[{objectQualifier}DNNrocketLangMerge](RLang1.[XMLData],R1.[XMLData]))')

	set @rtnFields = ' R1.[ItemId] '
	set @rtnFields = @rtnFields + ',ISNULL([RLangIdx].XMLData,{databaseOwner}[{objectQualifier}DNNrocketLangMerge](RLang1.[XMLData],R1.[XMLData])) as [XMLData] '				


	set @rtnFields = @rtnFields + ',ISNULL(RLang1.[Lang],ISNULL(R1.[Lang],'''')) as [Lang] '	 
 
	set @rtnFields = @rtnFields + ',R1.[PortalId] '
	set @rtnFields = @rtnFields + ',R1.[ModuleId] '
	set @rtnFields = @rtnFields + ',R1.[TypeCode] '
	set @rtnFields = @rtnFields + ',R1.[GUIDKey] '
	set @rtnFields = @rtnFields + ',R1.[ModifiedDate] '
	set @rtnFields = @rtnFields + ',R1.[TextData] '
	set @rtnFields = @rtnFields + ',R1.[XrefItemId] '
	set @rtnFields = @rtnFields + ',R1.[ParentItemId] '
	set @rtnFields = @rtnFields + ',R1.[UserId] '


	IF (@PageSize > 0)
	BEGIN
			-- Do Paging
		SET @STMT = 'DECLARE @recct int '
		set @STMT = @STMT + ' SET @recct = ' + Convert(nvarchar(5),@RecordCount) 
		
		set @STMT = @STMT + '   DECLARE @lbound int, @ubound int '

		SET @pageNum = ABS(@pageNum)
		SET @pageSize = ABS(@pageSize)
		IF @pageNum < 1 SET @pageNum = 1
		IF @pageSize < 1 SET @pageSize = 1

		set @STMT = @STMT + ' SET @lbound = ' + convert(nvarchar(50),((@pageNum - 1) * @pageSize))
		set @STMT = @STMT + ' SET @ubound = @lbound + ' + convert(nvarchar(50),(@pageSize + 1))
		set @STMT = @STMT + ' IF @lbound >= @recct BEGIN '
		set @STMT = @STMT + '   SET @ubound = @recct + 1 '
		set @STMT = @STMT + '   SET @lbound = @ubound - (' + convert(nvarchar(50),(@pageSize + 1)) + ') ' -- return the last page of records if no records would be on the specified page '
		set @STMT = @STMT + ' END '
		
		-- Default order by clause
		if @OrderBy = '' 
		Begin
			set @OrderBy = ' ORDER BY R1.ModifiedDate DESC '
		End
		
		set @STMT = @STMT + ' SELECT '
		if @ReturnLimit > 0 
		begin
			set @STMT = @STMT + ' top ' + convert(nvarchar(10),@ReturnLimit)
		end
		
		set @STMT = @STMT + @rtnFields		

		set @STMT = @STMT + ' FROM    (
								SELECT  ROW_NUMBER() OVER(' + @orderBy + ') AS row, '
		set @STMT = @STMT + @rtnFields		
		set @STMT = @STMT + ' FROM {databaseOwner}[{objectQualifier}DNNrocket]  as R1 '
		set @STMT = @STMT + ' left join {databaseOwner}[{objectQualifier}DNNrocket] as RLang1 on RLang1.ParentItemId = R1.ItemId and RLang1.[Lang] = ''' + @Lang + '''  and RLang1.TypeCode =  R1.TypeCode + ''LANG'' ' 
		set @STMT = @STMT + ' left join {databaseOwner}[{objectQualifier}DNNrocket] as [RLangIdx] on [RLangIdx].ParentItemId = R1.ItemId and [RLangIdx].[Lang] = ''' + @Lang + '''  and [RLangIdx].TypeCode =  R1.TypeCode + ''LANGIDX'' ' 
		
		set @STMT = @STMT + @JoinIndex

				IF (RIGHT(@TypeCode,1) = '%')
			BEGIN
				set @STMT = @STMT + 'WHERE R1.TypeCode Like ''' + @TypeCode + ''' ' + @Filter  
			END ELSE
			BEGIN
				IF (@TypeCode = '')
				BEGIN
					set @STMT = @STMT + 'WHERE R1.TypeCode != ''''' + @Filter  
				END ELSE
				BEGIN
					set @STMT = @STMT + 'WHERE R1.TypeCode = ''' + @TypeCode + ''' ' + @Filter  
				END
			END	                                                              
			
			set @STMT = @STMT + ' ) AS R1 '
			set @STMT = @STMT + ' left join {databaseOwner}[{objectQualifier}DNNrocket] as RLang1 on RLang1.ParentItemId = R1.ItemId and RLang1.[Lang] = ''' + @Lang + '''  and RLang1.TypeCode =  R1.TypeCode + ''LANG'' ' 
			set @STMT = @STMT + ' left join {databaseOwner}[{objectQualifier}DNNrocket] as [RLangIdx] on [RLangIdx].ParentItemId = R1.ItemId and [RLangIdx].[Lang] = ''' + @Lang + '''  and [RLangIdx].TypeCode =  R1.TypeCode + ''LANGIDX'' ' 
			set @STMT = @STMT + ' WHERE row > @lbound AND row < @ubound '

			
	END ELSE
	BEGIN
		-- DO NON-PAGING

		set @STMT = ' SELECT '
		if @ReturnLimit > 0 
		begin
			set @STMT = @STMT + ' top ' + convert(nvarchar(10),@ReturnLimit)
		end
		
		set @STMT = @STMT + @rtnFields		

		set @STMT = @STMT + ' FROM {databaseOwner}[{objectQualifier}DNNrocket]  as R1 '
		set @STMT = @STMT + ' left join {databaseOwner}[{objectQualifier}DNNrocket] as RLang1 on RLang1.ParentItemId = R1.ItemId and RLang1.[Lang] = ''' + @Lang + '''  and RLang1.TypeCode =  R1.TypeCode + ''LANG'' ' 
		set @STMT = @STMT + ' left join {databaseOwner}[{objectQualifier}DNNrocket] as [RLangIdx] on [RLangIdx].ParentItemId = R1.ItemId and [RLangIdx].[Lang] = ''' + @Lang + '''  and [RLangIdx].TypeCode =  R1.TypeCode + ''LANGIDX'' ' 
	
		set @STMT = @STMT + @JoinIndex

		IF (RIGHT(@TypeCode,1) = '%')
		BEGIN
			set @STMT = @STMT + 'WHERE R1.TypeCode Like ''' + @TypeCode + ''' ' + @Filter  
		END ELSE
		BEGIN
			IF (@TypeCode = '')
			BEGIN
				set @STMT = @STMT + 'WHERE R1.TypeCode != ''''' + @Filter  
			END ELSE
			BEGIN
				set @STMT = @STMT + 'WHERE R1.TypeCode = ''' + @TypeCode + ''' ' + @Filter  
			END
		END	                                                              	

		set @STMT = @STMT + ' ' + @OrderBy                                                           	

	END		



	EXEC sp_executeSQL @STMT                 -- return requested records

end

GO


if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}[{objectQualifier}DNNrocket_GetListCount]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
drop procedure {databaseOwner}[{objectQualifier}DNNrocket_GetListCount]
GO


CREATE    PROCEDURE {databaseOwner}[{objectQualifier}DNNrocket_GetListCount] 
@PortalId int, 
@ModuleId int,
@TypeCode nvarchar(50),
@Filter nvarchar(1000),
@Lang nvarchar(50),
@RecordCount int OUTPUT
AS
begin

	SET @Filter = REPLACE(@Filter,'[XMLData]','{databaseOwner}[{objectQualifier}DNNrocketLangMerge](RLang1.[XMLData],R1.[XMLData])')


------------------------------------------------------------------------------------
--- Cursor for INDEX records.
------------------------------------------------------------------------------------
DECLARE @idxname nvarchar(250) = ''
DECLARE @TCode nvarchar(250) = ''
DECLARE @idxparentItemId int = 0
DECLARE @systemlinkItemId int = 0
DECLARE @JoinIndex nvarchar(max)

select @systemlinkItemId = itemid from {databaseOwner}[{objectQualifier}DNNrocket] where [TypeCode] = 'SYSTEMLINK' and GUIDkey = @TypeCode 

--print '@systemlinkItemId: ' + convert(nvarchar(max),@systemlinkItemId)

DECLARE idx_cursor CURSOR FOR 
SELECT GUIDKey, ParentItemId, textdata
FROM {databaseOwner}[{objectQualifier}DNNrocket]
WHERE [TypeCode] like 'SYSTEMLINK%' and ParentItemId = @systemlinkItemId

SET @JoinIndex = ''

OPEN idx_cursor  
FETCH NEXT FROM idx_cursor INTO @idxname, @idxparentItemId, @TCode

WHILE @@FETCH_STATUS = 0  
BEGIN  

	if (CHARINDEX(@idxname, @Filter) > 0)
	begin

		set @JoinIndex = @JoinIndex + ' left join {databaseOwner}[{objectQualifier}DNNrocket] as [' + @idxname + '] on [' + @idxname + '].XrefItemId = R1.ItemId and [' + @idxname + '].TypeCode = ''IDX_' + @idxname + ''' and ([' + @idxname + '].[Lang] = ''' + @Lang + ''' or [' + @idxname + '].[Lang] = '''') '

	end

	FETCH NEXT FROM idx_cursor INTO @idxname,@idxparentItemId, @TCode
END 

CLOSE idx_cursor
DEALLOCATE idx_cursor

--print '@JoinIndex: ' + @JoinIndex

------------------------------------------------------------------------------------
------------------------------------------------------------------------------------



	--DECLARE @RecordCount int
	DECLARE @countselect nvarchar(max)

			set @countselect = ' SELECT '
	
			set @countselect = @countselect + ' @RecordCount=count(R1.ItemId) '	

			set @countselect = @countselect + ' FROM {databaseOwner}[{objectQualifier}DNNrocket]  as R1 '
			set @countselect = @countselect + ' left join {databaseOwner}[{objectQualifier}DNNrocket] as RLang1 on RLang1.ParentItemId = R1.ItemId and RLang1.[Lang] = ''' + @Lang + '''  and RLang1.TypeCode =  R1.TypeCode + ''LANG'' ' 
	
			set @countselect = @countselect + @JoinIndex

			IF (RIGHT(@TypeCode,1) = '%')
			BEGIN
				set @countselect = @countselect + 'WHERE R1.TypeCode Like ''' + @TypeCode + ''' ' + @Filter  
			END ELSE
			BEGIN
				IF (@TypeCode = '')
				BEGIN
					set @countselect = @countselect + 'WHERE R1.TypeCode != ''''' + @Filter  
				END ELSE
				BEGIN
					set @countselect = @countselect + 'WHERE R1.TypeCode = ''' + @TypeCode + ''' ' + @Filter  
				END
			END	                                                              	

		exec sp_executesql @countselect, N'@RecordCount int output', @RecordCount output;

		SELECT @RecordCount AS RecordCount       -- return the total # of records

END
GO




GO
SET QUOTED_IDENTIFIER OFF 
GO
SET ANSI_NULLS ON 
GO


